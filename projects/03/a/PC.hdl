// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:

    Inc16 (in=result, out=increm);
    Mux16 (a=result, b=increm, sel=inc, out=incRes);
    Mux16 (a=incRes, b=in, sel=load, out=newVal);
    Mux16 (a=newVal, b=false, sel=reset, out=regIn);

    Or (a=reset, b=inc, out=resOrInc);
    Or (a=resOrInc, b=load, out=regLoad);

    Register (in=regIn, load=regLoad, out=result, out=out);
}
